#Configure output
set term postscript enhanced solid color portrait
set output 'rayleighSimulation.eps'

#Define fit function
f(x) = ( a**n + (x)**n )**(1.0/n) + C

#Do the Levenberg-Marquardt fit, seeds of 1,1,1
fit f(x) 'rayleighSimulation.dat' u 1:3 via a,C,n 

#Make pretty strings for plot legends
FitParamsName = "Fit: ( a^n + x^n )^{(1/n)}"
FitParams = sprintf("Fit: ( a^n + x^n )^{(1/n)} + C\na = %.4f\nn =%.3f\nC = %.4f", a, n,C)

#We're going to make three plots in one output window
set multiplot layout 3,1

#First plot
set key left
set ylabel 'sigma units'
set xlabel 'offset (sigma)'
plot\
	'rayleighSimulation.dat' u 1:3 w p pt 7 lc 3 tit '95% upper limit',\
	'rayleighSimulation.dat' u 1:4 w p pt 7 lc 1 tit 'mean radius',\
	'rayleighSimulation.dat' u 1:1 w l lt 0      tit 'injected offset',\
	'rayleighSimulation.dat' u 1:2 w p pt 7 lc 6 tit '95% lower limit',\
	 f(x) tit FitParamsName lt 5

#Second plot
set key right
plot\
	'rayleighSimulation.dat' u 1:($3-f($1)) w p pt 7 lc 3 tit 'fit residuals'
set key left

#Third Plot
plot [0:2]\
	'rayleighSimulation.dat' u 1:3  w p pt 7 lc 3 tit '95% upper limit',\
	 f(x) tit FitParams lt 5

#Finish plot page
unset multiplot
